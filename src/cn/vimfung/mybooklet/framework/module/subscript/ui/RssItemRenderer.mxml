<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				width="100%" height="70"
				autoDrawBackground="true"
				dataChange="dataChangeHandler(event)" 
				creationComplete="dataChangeHandler(event)"
				click="itemclickHandler(event)">
	<fx:Script>
		<![CDATA[
			import cn.vimfung.common.db.SqliteDatabaseToken;
			import cn.vimfung.common.rss.Rss;
			import cn.vimfung.mybooklet.framework.GNFacade;
			import cn.vimfung.mybooklet.framework.module.subscript.notification.SubscriptNotification;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			private var _facade:GNFacade = GNFacade.getInstance();
			
			/**
			 * 数据变更
			 * 
			 * @param event 事件
			 * */
			protected function dataChangeHandler(event:FlexEvent):void
			{
				if (this.initialized)
				{
					var item:* = this.data;
					if (item != null)
					{
						if (item.image != null && item.image != "")
						{
							iconImage.source = item.image;
						}
						else
						{
							iconImage.source = "assets/gnote/rss_default.png";
						}
						titleLabel.text = item.title;
						descRichText.text = item.desc;
						checkBox.selected = item.issubscript > 0 ? true : false;
						this.refreshCheckBoxState();
					}
				}
			}
			
			/**
			 * 删除订阅源按钮点击
			 * 
			 * @param event 事件
			 * */
			protected function delButtonClickHandler(event:MouseEvent):void
			{
				var self:RssItemRenderer = this;
				
				_facade.alert("是否删除该订阅源?","提示","确定", "取消", function (event:CloseEvent):void{
					if (event.detail == Alert.OK)
					{
						//删除订阅源
						_facade.documentDatabase.beginTrans();
						try
						{
							var params:Dictionary = new Dictionary();
							params[":rssId"] = self.data.id;
							
							//移除我的订阅
							var token:SqliteDatabaseToken = _facade.documentDatabase.createCommandToken("DELETE FROM subscripts WHERE rssId = :rssId", params);
							token.startSync();
							//移除订阅缓存
							token = _facade.documentDatabase.createCommandToken("DELETE FROM rss_items WHERE rssId = :rssId", params);
							token.startSync();
							//移除订阅源
							token = _facade.documentDatabase.createCommandToken("DELETE FROM rss_list WHERE id = :rssId", params);
							token.startSync();
							
							_facade.documentDatabase.commitTrans();
							
							//派发移除通知
							var notif:SubscriptNotification = new SubscriptNotification(SubscriptNotification.REMOVE_RSS, self.data);
							_facade.postNotification(notif);
						}
						catch(err:Error)
						{
							_facade.documentDatabase.rollbackTrans();
						}
					}
				});
			}
			
			/**
			 * 刷新复选框状态
			 * */
			private function refreshCheckBoxState():void
			{
				if (checkBox.selected)
				{
					checkBox.label = "已订阅";
					checkBox.setStyle("color", "0x009900");
				}
				else
				{
					checkBox.label = "未订阅";
					checkBox.setStyle("color", "0x000000");
				}
			}
			
			/**
			 * 订阅复选框状态变更
			 * 
			 * @param event 事件
			 * */
			protected function checkBoxChangeHandler(event:Event):void
			{
				if (checkBox.selected)
				{
					this.data.issubscript = 1;
				}
				else
				{
					this.data.issubscript = 0;
				}
				this.refreshCheckBoxState();
				
			}
			
			/**
			 * 订阅项点击事件
			 * 
			 * @param event 事件对象
			 * */
			protected function itemclickHandler(event:MouseEvent):void
			{
				checkBox.selected = !checkBox.selected;
				checkBoxChangeHandler(null);
			}
			
			/**
			 * 订阅复选框点击
			 * 
			 * @param event 事件对象
			 * */
			protected function checkBoxclickHandler(event:MouseEvent):void
			{
				//防止相应父级点击事件，导致选中状态无法变更。
				event.stopImmediatePropagation();
			}
			
		]]>
	</fx:Script>
	<s:Rect left="0" right="0" top="0" bottom="0">
		<s:fill>
			<s:SolidColor color="0xf2f2f2" />
		</s:fill>
	</s:Rect>
	<s:VGroup width="100%" height="100%" paddingBottom="3" paddingTop="3" paddingLeft="3" paddingRight="3" verticalAlign="middle" horizontalAlign="center">
		<s:Image id="iconImage" fillMode="scale" width="100%" />
		<s:Label id="titleLabel" />
		<s:RichText id="descRichText" width="100%" height="100%" fontSize="12" lineBreak="toFit"
					maxDisplayedLines="3" verticalAlign="top" color="0x999999" textAlign="center" />
		<s:HGroup verticalAlign="middle">
			<s:CheckBox id="checkBox" label="未订阅" change="checkBoxChangeHandler(event)" click="checkBoxclickHandler(event)" />
			<s:Label text=" | " />
			<s:Button label="删除" click="delButtonClickHandler(event)" />
		</s:HGroup>
		
	</s:VGroup>
</s:ItemRenderer>
